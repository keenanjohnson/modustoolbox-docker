#
# Cypress Semiconductor KitProg3
#
# KitProg3 is CMSIS-DAP compliant adapter. However, it supports additional functionality
# such as SPI/I2C bridging, Hardware acquire procedure for PSoC 4/5/6 devices, power control.
# This functionality has been moved to separate driver to avoid pollution of generic CMSIS-DAP
# with probe-specific code.
#
# Interface driver inherits all functionality of CMSIS-DAP including all tcl commands.
# Additional functionality can be accessed via tcl using 'kitprog3' prefix
#

adapter driver kitprog3

proc get_kp3_version {kp3_fw_dir} {
	set result ""
	set ver_file ""
	catch { set ver_file [find "${kp3_fw_dir}/kitprog3.version"] }
	if { $ver_file != "" } {
		set fd [open $ver_file "r"]
		set ver [read -nonewline $fd]
		close $fd
		regsub -all {\s} $ver "" ver
		regsub -all {</?version>} $ver "" ver
		set result $ver
	}
	return $result
}

#
# Get the MTB tool path by the given MTBQuery tool ID.
# This function invokes the mtbquery executable passing the tool ID as an argument,
# then extracts the tool's 'path' entry from its output and returns it.
# See for details PROGTOOLS-3023 - 'OpenOCD : Integration with MTB Query API'
#
proc get_tool_dir {tools_dir tool_id} {
	set tool_dir ""
	set query_args "--toolinfo $tool_id"
	set status [catch {exec $tools_dir/mtbquery/mtbquery {*}$query_args} query_out]
	if { $status == 0 } {
		regexp {path\s:\s(.*)$} $query_out -> tool_dir
	}
	return $tool_dir
}


set script_dir [file dirname [file join [pwd] [info script]]]
set script_dir_list [file split $script_dir]

if {[llength $script_dir_list] > 5} {
	set tools_dir [file join {*}[lrange [file split $script_dir] 0 end-5]]

	# Get the latest fw-loader tool in the MTB 3.x environment (tools_3.x/ folder)
	set fw_loader_dir [get_tool_dir $tools_dir "1901ec91-2683-4ab4-8034-211b772c9a2b"]

	# Get the latest KP firmware asset in the MTB 3.x environment (tools_3.x/ folder)
	set kp3_fw_dir [get_tool_dir $tools_dir "2bd09d79-2620-45fd-9452-77d1dbfe41d5"]

	set kp3_fw_ver [get_kp3_version $kp3_fw_dir]
	if { $fw_loader_dir != "" && $kp3_fw_ver != "" } {
		echo "kitprog3 set_latest_version: $fw_loader_dir $kp3_fw_ver"
		kitprog3 set_latest_version $fw_loader_dir $kp3_fw_ver
	}
}
